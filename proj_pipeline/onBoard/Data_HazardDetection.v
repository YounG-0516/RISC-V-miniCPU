`include "defines.vh"

module Data_HazardDetection(
    input wire [4 :0] rR1_ID,
    input wire [4 :0] rR2_ID,

    input wire        ID_read1,
    input wire        ID_read2,
    input wire [1 :0] rf_wsel,

    input wire [4 :0] wR_EX,
    input wire [4 :0] wR_MEM,
    input wire [4 :0] wR_WB,

    input wire [31:0] wD_EX,
    input wire [31:0] wD_MEM,
    input wire [31:0] wD_WB,

    input wire        rf_we_EX,
    input wire        rf_we_MEM,
    input wire        rf_we_WB,
    
    //å‰é?’ç›¸å…³ä¿¡å?
    output wire       forward_op1,
    output wire       forward_op2,
    output reg [31:0] rD1_forward,
    output reg [31:0] rD2_forward,

    //åœé¡¿åŠæ¸…é™¤ä¿¡å?
    output reg        pipeline_stop_PC,
    output reg        pipeline_stop_REG_IF_ID,
    output reg        flush_REG_ID_EX
);

    /**
     * æ•°æ®å†’é™©æ£?æµ‹ï¼šæ ¸å¿ƒæ¡ä»¶ï¼šEX/MEM/WB_wR==rR1/rR2ï¼›é™„åŠ çš„æ¡ä»¶ï¼šæ­¤é˜¶æ®µRFå¯å†™ï¼ŒrD1/rD2å¿…é¡»è¢«ä½¿ç”¨ï¼ŒwRä¸èƒ½ä¸ºx0ã€?
     */

    //RAW-A 
    wire RAW_A_rR1 = (wR_EX == rR1_ID) && rf_we_EX && ID_read1 && (wR_EX != 5'b0);
    wire RAW_A_rR2 = (wR_EX == rR2_ID) && rf_we_EX && ID_read2 && (wR_EX != 5'b0);

    //RAW-B
    wire RAW_B_rR1 = (wR_MEM == rR1_ID) && rf_we_MEM && ID_read1 && (wR_MEM != 5'b0);
    wire RAW_B_rR2 = (wR_MEM == rR2_ID) && rf_we_MEM && ID_read2 && (wR_MEM != 5'b0);

    //RAW-C
    wire RAW_C_rR1 = (wR_WB == rR1_ID) && rf_we_WB && ID_read1 && (wR_WB != 5'b0);
    wire RAW_C_rR2 = (wR_WB == rR2_ID) && rf_we_WB && ID_read2 && (wR_WB != 5'b0);

    //load_use
    wire load_use_hazard = (RAW_A_rR1 || RAW_A_rR2) && (rf_wsel == `WB_DREM);


    /**
     * æ•°æ®å†’é™©è§£å†³ï¼šå‰é€?+åœé¡¿
     */

    //åˆ¤æ–­æ˜¯å¦éœ?è¦å‰é€?:RAWä»»æ„å†’é™©å‡ºç°å³éœ€è¦å‰é€?
    assign forward_op1 = RAW_A_rR1 | RAW_B_rR1 | RAW_C_rR1;
    assign forward_op2 = RAW_A_rR2 | RAW_B_rR2 | RAW_C_rR2;

    //è®¡ç®—å‰é?’æ•°æ?
    always @ (*) begin
        if (RAW_A_rR1)      rD1_forward = wD_EX;
        else if (RAW_B_rR1) rD1_forward = wD_MEM;
        else if (RAW_C_rR1) rD1_forward = wD_WB;
        else                rD1_forward = 32'b0;
    end

    always @ (*) begin
        if (RAW_A_rR2)      rD2_forward = wD_EX;
        else if (RAW_B_rR2) rD2_forward = wD_MEM;
        else if (RAW_C_rR2) rD2_forward = wD_WB;
        else                rD2_forward = 32'b0;
    end

    //load_useå†’é™©éœ?è¦ç‰¹æ®Šå¤„ç†ï¼šåœé¡¿(PCåŠIF/IDä¸å˜+ID/EXç½?0)+å‰é??
    always @ (*) begin
        if(load_use_hazard)  begin
            pipeline_stop_PC = 1'b1;
            pipeline_stop_REG_IF_ID = 1'b1;
            flush_REG_ID_EX = 1'b1;
        end
        else begin
            pipeline_stop_PC = 1'b0;
            pipeline_stop_REG_IF_ID = 1'b0;
            flush_REG_ID_EX = 1'b0;
        end
    end


endmodule